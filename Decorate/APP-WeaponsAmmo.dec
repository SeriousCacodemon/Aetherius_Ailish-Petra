// Ailish specific version of the Serpent Staff from Hexen.  Runs off of MP
// instead of Blue Mana...

actor RoyalScepter : CWeapStaffDVDS { Tag "Royal Scepter" }
actor SoulWhisper : CWeapStaffDVDS { Tag "Soul Whisper" }
actor ShockStaff : CWeapStaffDVDS { Tag "Xuric's Shock Staff" }
actor Histanica : CWeapStaffDVDS { Tag "Histanica" }
actor Batholith : CWeapStaffDVDS { Tag "Batholith" }
actor WizardWoodStave : CWeapStaffDVDS { Tag "Wizardwood Stave" }
actor ChilltipCane : CWeapStaffDVDS { Tag "Chilltip Cane" }
actor NightShadow : CWeapStaffDVDS { Tag "Nightshadow" }

actor SerpentStaffAilish : CWeapStaffDVDS
{
	Inventory.PickupMessage "You got the Serpent Staff!"
	Obituary "$OB_MPCWEAPSTAFFM"
	Tag "Serpent Staff"
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 0
	Weapon.AmmoType "None"
	States
	{
		Select:
			CSSF C 1 A_Raise(12)
			Loop
		Deselect:
			CSSF B 3
			CSSF C 4
			CSSF C 1 A_Lower(12)
			Wait
		Fire:
			CSSF A 1 Offset (0, 45) A_CStaffCheck
			CSSF J 0 
			{
				A_UseManaAttack(1,2);
				if (CountInv("CanUseManaAttack", AAPTR_PLAYER1) == 0) { return state("Ready"); }
				return state("");
			}
			CSSF J 0 ACS_NamedExecuteAlways("StartTensionCountDown")
			CSSF J 1 Offset (0, 50) A_CStaffAttack
			CSSF J 2 Offset (0, 50)
			CSSF J 2 Offset (0, 45)
			CSSF A 2 Offset (0, 40)
			CSSF A 2 Offset (0, 36)
			Goto Ready + 2
		Drain:
			CSSF K 0 
			{
				A_UseManaAttack(1,2);
				if (CountInv("CanUseManaAttack", AAPTR_PLAYER1) == 0) { return state("Ready"); }
				return state("");
			}
			CSSF K 0 ACS_NamedExecuteAlways("StartTensionCountDown")
			CSSF K 10 Offset (0, 36)
			Goto Ready + 2
	}
}

Actor CStaffMissileNew : AetheriusProjectileZSC // Replaces CStaffMissile
{
	Damage (finaldamagedealt)
	Speed 22
	Radius 12
	Height 10
	Damage 5
	RenderStyle Add
	Projectile
	DeathSound "ClericCStaffExplode"
	Obituary "$OB_MPCWEAPSTAFFR"
	States
	{
		Spawn:
			CSSF D 0 Bright NoDelay { A_GetPlayerAttackDamage(57,0); }
		Idle:
			CSSF DDEE 1 Bright A_Weave(3, 0, 1.0, 0.0)
			Loop
		Death:
			CSSF FG 4 Bright
			CSSF HI 3 Bright
			Stop
	}
}
// Petra-specfic version with more comboability...
// What HP amount do I equate 32000 with?  Belphegor specific HP?
Actor EtherStock : Ammo
{
	Inventory.PickupMessage "Picked up Ether Stock (3 units)."
	Inventory.Icon "ACMFULL"
	Inventory.Amount 3
	Inventory.MaxAmount 12
	Ammo.BackPackAmount 3
	Ammo.BackPackMaxAmount 12
}

actor AChristina : AetheriusBaseWeaponZSC
{
	Inventory.PickupMessage "Found Alexandra Christina!  Ether-charged pew pew!"
	XScale 0.350
	YScale 0.292
	weapon.selectionorder 400
	Obituary "%o got blammed by %k's A. Christina."
	+SPAWNSOUNDSOURCE
	states
	{
		Select:
			ACHG A 1 A_Raise(6)
			loop
		Deselect:
			ACHG A 1 A_Lower(6)
			loop
		Fire:
			ACHG B 0 BRIGHT
			{
				ACS_NamedExecuteAlways("StartTensionCountDown");
				A_PlaySound("PetraHeavyAttack",CHAN_VOICE);
				A_PlaySound("AChristina/Shoot1",CHAN_WEAPON);
			}
			 ACHF A 1 BRIGHT
			{
				A_SpawnItemEx("AChristinaShot",45,5,34-pitch,0,0);
				A_SpreadAuraFireBullets(5,5,20,0,"null",0,112); //Allow standard fire to be able to set off shootable switches, doors, and walls
			}
			//ACHF A 1 BRIGHT A_FireCustomMissile("AChristinaShot",0,0,5)
			ACHG CDC 2
			ACHG A 12
			ACHG A 1 A_ReFire
			Goto Ready
		AltFire:
		AltHold:
			ACHG B 0 
			{
				A_CheckAChristinaCheck;
				if (CountInv("AChristinaAltFireType",AAPTR_PLAYER1) >= 1) { return state("BouncingShotWeak"); } // Enter bouncing shot state if Zoom is held
				return state("");
			}
			ACHG B 0 A_JumpIfInventory("EtherStock",1,"AltHoldCharged")
			ACHG B 0 BRIGHT
			{
				A_PlayWeaponSound("AChristina/LongRangeShoot");
				ACS_NamedExecuteAlways("StartTensionCountDown");
			}
			ACHF B 1 A_SpreadAuraFireBullets(0,0,1,0,"AChristinaPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ)//1024,"",0,0)
			ACHG FGF 1
			ACHG A 12
			ACHG A 1 A_ReFire
			Goto Ready
		AltHoldCharged:
			ACHG B 0
			{
				A_PlaySound("PetraRangedAttackVoice1",CHAN_VOICE);
				A_PlaySound("AChristina/LongRangeShoot",CHAN_WEAPON);			
				ACS_NamedExecuteAlways("StartTensionCountDown");
			}
			ACHF A 0 A_TakeInventory("EtherStock",1)
			ACHF A 1 A_SpreadAuraFireBullets(0,0,1,0,"AChristinaPuffCharged",FBF_NORANDOM|FBF_NORANDOMPUFFZ)//1024,"",0,0)
			ACHG CDC 1
			ACHG A 8
			ACHG B 0
			{
				A_PlaySound("PetraRangedAttackVoice2",CHAN_VOICE);
				A_PlaySound("AChristina/LongRangeShoot",CHAN_WEAPON);			
			}
			ACHF A 1 A_SpreadAuraFireBullets(0,0,1,0,"AChristinaPuffCharged",FBF_NORANDOM|FBF_NORANDOMPUFFZ)//1024,"",0,0)
			ACHG CDC 1
			ACHG A 8
			ACHG A 0 A_JumpIfInventory("EtherStock",1,2)
			ACHG A 0 A_Refire("AltFire")
			goto Ready
			ACHG A 1 A_Refire("AltHoldCharged2")
			goto Ready
		AltHoldCharged2:
			ACHG B 0
			{
				A_PlaySound("PetraRangedAttackVoice3",CHAN_VOICE);
				A_PlaySound("AChristina/LongRangeShoot",CHAN_WEAPON);			
			}
			ACHF A 0 A_TakeInventory("EtherStock",1)
			ACHF B 1 A_SpreadAuraFireBullets(0,0,1,0,"AChristinaPuffCharged",FBF_NORANDOM|FBF_NORANDOMPUFFZ)//1024,"",0,0)
			ACHG FGF 1
			ACHG A 8
			ACHG B 0 A_PlaySound("AChristina/LongRangeShoot",CHAN_WEAPON)			
			ACHF A 1 A_SpreadAuraFireBullets(0,0,1,0,"AChristinaPuffCharged",FBF_NORANDOM|FBF_NORANDOMPUFFZ)//1024,"",0,0)
			ACHG CDC 1
			ACHG A 8
			ACHG A 0 A_JumpIfInventory("EtherStock",1,2)
			ACHG A 0 A_Refire("AltFire")
			goto Ready
			ACHG A 1 A_Refire("AltHoldCharged3")
			goto Ready
		AltHoldCharged3:
			ACHG B 0
			{
				A_PlaySound("PetraRangedAttackVoice4",CHAN_VOICE);
				A_PlaySound("AChristina/LongRangeShoot",CHAN_WEAPON);			
			}
			ACHF A 0 A_TakeInventory("EtherStock",1)
			ACHF B 1 A_SpreadAuraFireBullets(0,0,1,0,"AChristinaPuffChargedFinal",FBF_NORANDOM|FBF_NORANDOMPUFFZ)//1024,"",0,0)
			ACHG FGF 1
			ACHG A 24
			ACHG A 0 A_Refire
			goto Ready
		BouncingShotWeak:
			ACHG A 0 A_JumpIfInventory("EtherStock",1,"BouncingShotStage1")
			ACHG A 5
			ACHF A 0
			{
				A_PlaySound("PetraRangedAttackVoice1",CHAN_VOICE);
				A_PlaySound("AChristina/LongRangeShoot",CHAN_WEAPON);			
				A_TakeInventory("EtherStock",1);
				ACS_NamedExecuteAlways("StartTensionCountDown");
			}
			ACHF B 1 A_FireCustomMissile("AChristinaBouncingShotWeak",0,0,-3)
			ACHG FGF 1
			ACHG A 24
			ACHG A 0 A_Refire
			goto Ready
		BouncingShotStage1:
			ACHG A 4
			ACHF A 0 A_JumpIfInventory("EtherStock",2,1)
			goto BouncingShotStage1+3
			ACHF A 1 A_ReFire("BouncingShotStage2")
			ACHF A 0
			{
				A_PlaySound("PetraRangedAttackVoice2",CHAN_VOICE);
				A_PlaySound("AChristina/LongRangeShoot",CHAN_WEAPON);			
				A_TakeInventory("EtherStock",1);
				ACS_NamedExecuteAlways("StartTensionCountDown");
			}
			ACHF B 1 A_FireCustomMissile("AChristinaBouncingShotStage1",0,0,-3)
			ACHG FGF 1
			ACHG A 24
			ACHG A 0 A_Refire
			goto Ready

		BouncingShotStage2:
			ACHG A 3
			ACHF A 0 A_JumpIfInventory("EtherStock",3,1)
			goto BouncingShotStage2+3
			ACHF A 1 A_ReFire("BouncingShotStage3")
			ACHF A 0
			{
				A_PlaySound("PetraRangedAttackVoice3",CHAN_VOICE);
				A_PlaySound("AChristina/LongRangeShoot",CHAN_WEAPON);			
				A_TakeInventory("EtherStock",2);
				ACS_NamedExecuteAlways("StartTensionCountDown");
			}
			ACHF B 1 A_FireCustomMissile("AChristinaBouncingShotStage2",0,0,-3)
			ACHG FGF 1
			ACHG A 24
			ACHG A 0 A_Refire
			goto Ready
		BouncingShotStage3:
			ACHG A 2
			ACHF A 0
			{
				A_PlaySound("PetraRangedAttackVoice4",CHAN_VOICE);
				A_PlaySound("AChristina/LongRangeShoot",CHAN_WEAPON);			
				A_TakeInventory("EtherStock",3);
				ACS_NamedExecuteAlways("StartTensionCountDown");
			}
			ACHF B 1 A_FireCustomMissile("AChristinaBouncingShotStage3",0,0,-3)
			ACHG FGF 1
			ACHG A 24
			ACHG A 0 A_Refire
			goto Ready

		Reload:
			ACHG H 0 A_PlaySound("AChristina/Reload1",CHAN_5)
			ACHG HHH 4 A_GiveInventory("EtherStock",1)
			ACHG H 0
			{
				A_CheckAChristinaCheck;
				if (CountInv("AChristinaAltFireType",AAPTR_PLAYER1) == 3) { return state("Reload2"); } // Enter bouncing shot state if Zoom is held
				return state("");
			}
			goto ReloadEnd
		Reload2:
			ACHG H 4 A_GiveInventory("EtherStock",1)
			ACHG H 0 A_JumpIfInventory("EtherStock",12,"ReloadEnd")
			ACHG H 0
			{
				A_CheckAChristinaCheck;
				if (CountInv("AChristinaAltFireType",AAPTR_PLAYER1) == 3) { return state("Reload2"); } // Enter bouncing shot state if Zoom is held
				return state("");
			}
			goto ReloadEnd
		ReloadEnd:
			ACHG A 4
			{
				A_PlaySound("PetraReloadVoice",CHAN_VOICE);
				A_PlaySound("AChristina/Reload2",CHAN_WEAPON);
			}
			ACHG IJI 4
			ACHG A 6
			goto Ready
		Ready:
			ACHG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER1)
			Loop
		Spawn:
			VUPP A -1 Bright
			stop
	}
}

Actor AChristinaCyberdemon : AChristina { Inventory.PickupMessage "Alexandra Christina gains ability to fire Cyberdemon rockets!" }
Actor AChristinaMastermind : AChristina { Inventory.PickupMessage "Alexandra Christina gains ability to fire like Spider Mastermind's Super Chaingun!" }
Actor AChristinaHades : AChristina { Inventory.PickupMessage "Alexandra Christina gains ability to use Hades Elemental/Overlord's dark lightning!" }
Actor AChristinaIronLich : AChristina { Inventory.PickupMessage "Alexandra Christina gains ability to conjure Iron Lich's spells!" }
Actor AChristinaMaulotaur : AChristina { Inventory.PickupMessage "Alexandra Christina gains ability to use Maulotaur's magical fire!" }
Actor AChristinaWyvern : AChristina { Inventory.PickupMessage "Alexandra Christina gains ability to use Death Wyvern's exploding fireballs!" }
Actor AChristinaHeresiarch : AChristina { Inventory.PickupMessage "Alexandra Christina gains ability to use Heresiarch magic (minus the barrier)!" }

actor AChristinaShot : AetheriusProjectileZSC
{
	Radius 11
	Height 8
	XScale 0.26 // 0.24
	YScale 0.208 // 0.20
	Speed 0
	+FORCERADIUSDMG
	DamageType "Ether"
	Projectile
	Renderstyle "Add"
	SeeSound ""
	DeathSound ""
	Obituary "%o got blammed by %k's A. Christina."
	States
	{
		Spawn:
			CHRB A 1 BRIGHT
		Death:
			CHRB A 1 BRIGHT 
			{
				A_GetPlayerAttackDamage(48,0);
				A_Explode((finaldamagedealt),64,0,1,64);
			}
			CHRB AAAAAAAAAA 1 BRIGHT A_FadeOut(0.1)
			stop
	}
}

/*actor AChristinaShot2 : AChristinaShot
{
	States
	{
		Spawn:
			CHRP A 1 BRIGHT
		Death:
			CHRP A 1 BRIGHT
			{
				A_GetPlayerAttackDamage(48,0);
				A_Explode((finaldamagedealt),64,0,1,64);
			}
			CHRP A 1 BRIGHT A_FadeOut(0.1)
			goto Death+2
	}
}*/

Actor AChristinaTracer : AetheriusFastProjectileZSC
{
	Damagetype "Ether"
	Radius 1
	Height 1
	Mass 5
	Speed 160 // 80
	PROJECTILE
	+DONTREFLECT
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	Renderstyle Add
	Alpha 1.0
	Scale 0.5
	decal BulletChip
	States
	{
		Spawn:
			TNT1 A 0 NoDelay bright { A_ScaleVelocity(GetCVar("dvds_tracervelmulti")); }
		Idle:
			TNT1 A 0 bright
			TNT1 A 0 bright A_SpawnItemEx("ZombieBulletTracerTrail",-15.0,0,0,0,0,0,0,128,0)
			TNT1 A 0 bright A_SpawnItemEx("ZombieBulletTracerTrail",-12.5,0,0,0,0,0,0,128,0)
			TNT1 A 0 bright A_SpawnItemEx("ZombieBulletTracerTrail",-10.0,0,0,0,0,0,0,128,0)
			TNT1 A 0 bright A_SpawnItemEx("ZombieBulletTracerTrail",-7.5,0,0,0,0,0,0,128,0)
			TNT1 A 0 bright A_SpawnItemEx("ZombieBulletTracerTrail",-5.0,0,0,0,0,0,0,128,0)
			TNT1 A 0 bright A_SpawnItemEx("ZombieBulletTracerTrail",-2.5,0,0,0,0,0,0,128,0)
			TNT1 A 1 bright A_SpawnItemEx("ZombieBulletTracerTrail",0.0,0,0,0,0,0,0,128,0)
			loop
		Death:
			CHRP C 0 A_SetScale(0.5,0.418)
			CHRP C 1 BRIGHT 
			{
				A_GetPlayerAttackDamage(49,0);
				A_Explode((finaldamagedealt),32,0,1,32);
			}
			CHRP DEFGHIJKLMNOPQRSTUVW 1 BRIGHT
			stop
	}
}

actor AChristinaPuff : AetheriusPuffZSC
{
	Radius 12
	Height 8
	Alpha 1
	Renderstyle "Add"
	VSpeed 0
	Mass 0
	+NOBLOCKMAP
	+NOGRAVITY
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	+FORCERADIUSDMG
	DamageType "Ether"
	XScale 0.5 // 0.24
	YScale 0.418 // 0.20
	States
	{
		Spawn:
			CHRP C 0
			CHRP C 1 BRIGHT 
			{
				A_GetPlayerAttackDamage(49,1);
				A_Explode((finaldamagedealt),32,0,true,32,0,0,"BulletPuff","Ether");
			}
			CHRP DEFGHIJKLMNOPQRSTUVW 1 BRIGHT
			stop
	}
}

Actor AChristinaPuffCharged : AChristinaPuff
{
	States
	{
		Spawn:
			CHRP C 0
			CHRP C 1 BRIGHT
			{
				A_GetPlayerAttackDamage(53,1);
				A_Explode((finaldamagedealt),32,0,true,32,0,0,"BulletPuff","Ether");
			}
			CHRP DEFGHIJKLMNOPQRSTUVW 1 BRIGHT
			stop
	}
}

Actor AChristinaPuffChargedFinal : AChristinaPuff
{
	XScale 0.875
	YScale 0.7524

	States
	{
		Spawn:
			CHRP C 0
			CHRP C 1 BRIGHT
			{
				A_GetPlayerAttackDamage(54,1);
				A_Explode((finaldamagedealt),70,0,true,70,0,0,"BulletPuff","Ether");
			}
			CHRP DEFGHIJKLMNOPQRSTUVW 1 BRIGHT
			stop
	}
}

actor AChristinaBouncingShotWeak : AetheriusProjectileZSC
{
	Damage (finaldamagedealt)
	XScale 0.26
	YScale 0.208
	Radius 2
	Height 4
	Speed 15
	Alpha 1
	Renderstyle "Add"
	PROJECTILE
	+FORCEXYBILLBOARD
	+RIPPER
	States
	{
		Spawn:
			TNT1 A 0 Bright NoDelay { A_GetPlayerAttackDamage(55,0); }
		Idle:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 1 Bright
			{
				A_SpawnItemEx("AChristinaBouncingShotTrail",0,0,0-pitch);
				A_SpawnItemEx("AChristinaBouncingShotTrail",-4,0,0-pitch);
				A_SpawnItemEx("AChristinaBouncingShotTrail",-8,0,0-pitch);
				A_SpawnItemEx("AChristinaBouncingShotTrail",-12,0,0-pitch);
			}
			goto Death
		Death:
			TNT1 A 1
			stop
	}
}

actor AChristinaBouncingShotStage1 : AChristinaBouncingShotWeak
{
	Damage (finaldamagedealt)
	BounceType Doom
	BounceFactor 1
	WallBounceFactor 1
	BounceCount 2
	BounceSound "AChristina/BouncingShotBounce"
	+BOUNCEONWALLS
	+BOUNCEONFLOORS
	+BOUNCEONCEILINGS
	States
	{
		Spawn:
			TNT1 A 0 Bright NoDelay { A_GetPlayerAttackDamage(56,0); }
		Idle:
			TNT1 A 1 Bright
			{
				A_SpawnItemEx("AChristinaBouncingShotTrail",0,0,0-pitch);
				A_SpawnItemEx("AChristinaBouncingShotTrail",-4,0,0-pitch);
				A_SpawnItemEx("AChristinaBouncingShotTrail",-8,0,0-pitch);
				A_SpawnItemEx("AChristinaBouncingShotTrail",-12,0,0-pitch);
			}
			loop
		Death:
			TNT1 A 1
			stop
	}
}

actor AChristinaBouncingShotStage2 : AChristinaBouncingShotStage1 { BounceCount 3 }
actor AChristinaBouncingShotStage3 : AChristinaBouncingShotStage1 { BounceCount 4 }

actor AChristinaBouncingShotTrail : Actor
{
	XScale 0.16
	YScale 0.12
	Speed 0
	Alpha 1
	RenderStyle "Add"
	+NOGRAVITY
	States
	{
		Spawn:
			CHBS A 0
			CHBS A 1 Bright A_SetScale(0.20,0.16)
			CHBS A 1 Bright A_SetScale(0.24,0.2)
			CHBS A 1 Bright A_SetScale(0.20,0.16)
			goto Death
		Death:
			TNT1 A 0
			stop
	}
}

//Partinias shot addon.  Basically attacks used by the Partinias arcana.
//======================================================================
//To do later(?):
//Super version of both existing attacks perhaps?
Actor AChristinaPartinias : AChristina 
{
	Inventory.PickupMessage "Alexandra Christina gains ability to... use Partinias's heartful magic?!"
	DamageType "Love"
	AttackSound "PartiniasRozToxo" //Roz Toxo laser attack sound
	States
	{
		Fire:
			ACHG B 0 BRIGHT
			{
				ACS_NamedExecuteAlways("StartTensionCountDown");
				A_PlaySound("PetraHeavyAttack",CHAN_VOICE);
				A_PlaySound("AChristina/Shoot1",CHAN_WEAPON);
			}
			 ACHF A 1 BRIGHT
			{
				A_SpreadAuraFireProjectile("AChristinaPartiniasBall",0,0,5);
			}
			ACHG CDC 2
			ACHG A 12
			ACHG A 1 A_ReFire
			Goto Ready
		AltFire:
		AltHold:
			ACHG B 0 A_JumpIfInventory("EtherStock",1,"AltHoldCharged")
			ACHG B 0 BRIGHT
			{
				//A_PlayWeaponSound("AChristina/LongRangeShoot"); //Put a "Drawing a blank" sound here?
				ACS_NamedExecuteAlways("StartTensionCountDown");
			}
			ACHG A 1
			ACHG A 12
			ACHG A 1 A_ReFire
			Goto Ready
		AltHoldCharged:
			ACHF A 0 A_TakeInventory("EtherStock",1)
			ACHF B 1 A_RailAttack(random(117,234),-3,0, "","", 0,0,"BulletPuff",0.0,0.0, 8192,35,0.0,0.0,"PartiniasLaserSprite",4.0)
			//ACHF B 1 A_SpreadAuraRailAttack(random(117,234),-3,0,"","",0,0,"BulletPuff",0.0,0.0,8192,35,0,0,"PartiniasLaserSprite",4.0) Spreadfire Aura level 2 bogs down framerate temporarily at times with this...
			ACHG FGF 4
			ACHG A 16
			ACHG A 0 A_Refire
			goto Ready
	}
}

Actor AChristinaPartiniasBall : AetheriusProjectileZSC
{
	Damage (random(98,195)) //Damage (finaldamagedealt)
	DamageType "Love"
	XScale 0.26
	YScale 0.208
	Radius 6
	Height 8
	Speed 10
	Alpha 1
	Renderstyle "Add"
	PROJECTILE
	+FORCEXYBILLBOARD
	+SEEKERMISSILE
	SeeSound "PartiniasRozSfaira"
	States
	{
		Spawn:
			TNT1 A 0 Bright NoDelay // { A_GetPlayerAttackDamage(56,0); }
		Idle:
			//CHPA ABCDEFGHIJKL 3 Bright
			CHPA M 1 Bright { A_SetScale(0.26,0.208); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.255,0.204); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.25,0.2); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.245,0.196); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.24,0.192); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.235,0.188); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.23,0.184); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.225,0.18); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.22,0.176); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.225,0.18); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.23,0.184); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.235,0.188); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.24,0.192); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.245,0.196); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.25,0.2); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			CHPA M 1 Bright { A_SetScale(0.255,0.204); A_Weave(0,2,0,0.5); A_SeekerMissile(0,5); }
			loop
		Death:
			CHPE ABCDEFGHIJKL 1 Bright A_SetScale(0.26,0.208)
			stop
	}
}

Actor PartiniasLaserSprite : Actor
{
	XScale 0.195
	YScale 0.156
	Alpha 1
	Renderstyle "Add"
	+FORCEXYBILLBOARD
	+NOGRAVITY
	States
	{
		Spawn:
			CHPA N 6 Bright
			CHPA NNNNNNNNNNNNNNNNNNNN 1 Bright A_FadeOut(0.05)
			stop
	}
}
actor SauronGauntletsPetra : MarineWeapon
{
	var int user_theta;
	+BLOODSPLATTER
	Weapon.SelectionOrder 3400
	+WEAPON.MELEEWEAPON
	Weapon.KickBack 140
	Obituary "$o was decked to the curb by %k's gauntlets of Sauron."
	Tag "Sauron's Gauntlets"

	Scale 0.825
	+FLOATBOB
	+WEAPON.BFG
	+WEAPON.NOALERT
	Inventory.PickupMessage "'\ciWell, what do we have here?  I know I'm not a pugilist type, but...\c-'"
	AttackSound "GauntletHit"

	States
	{
/*
		DeggarisPickup:
			WGNT A 0 A_SpawnItemEx("SauronGauntlets",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0,tid)
			WGNT A 0 Thing_Remove(0)
			Loop
		IlluciaPickup:
			WGNT A 0 A_SpawnItemEx("SauronGauntletsIllucia",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0,tid)
			WGNT A 0 Thing_Remove(0)
			Loop
		FloraPickup:
			WGNT A 0 A_SpawnItemEx("SauronGauntletsFlora",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0,tid)
			WGNT A 0 Thing_Remove(0)
			Loop
		AilishPickup:
			WGNT A 0 A_SpawnItemEX("SauronGauntletsAilish",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0,tid)
			WGNT A 0 Thing_Remove(0)
			Loop
		PetraPickup:
			WGNT A 0 A_SpawnItemEX("SauronGauntletsPetra",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0,tid)
			WGNT A 0 Thing_Remove(0)
			Loop
		NoMore:
			WGNT A 0 Thing_Remove(0)
			Loop
*/
		Spawn:
			TNT1 A 0
			WGNT AAA 1
/*
			{
				if (GetCVar("dvds_saurongauntletsdrops") <= 0) 
				{ 
					Thing_Remove(0); 
				}
				else
				{
					A_GauntletsCheckCharacter;
							 if (CountInv("CharacterClassCheck") == 0) { return state("DeggarisPickup"); }
					else if (CountInv("CharacterClassCheck") == 1) { return state("IlluciaPickup"); }
					else if (CountInv("CharacterClassCheck") == 2) { return state("FloraPickup"); }
					else if (CountInv("CharacterClassCheck") == 9) { return state("AilishPickup"); }
					return state("");
				}
				return state("");
			}
*/
		SpawnSoundEmit:
			WGNT A 0 
			{
				A_Stopsound(CHAN_ITEM);
				A_Playsound("SauronGauntlets/Hum",CHAN_ITEM);
				A_SetInventory("ActorVar1",0);
			}
		SpawnPhase1:
			WGNT AA 1
/*
			{
				if (GetCVar("dvds_saurongauntletsdrops") <= 0) 
				{ 
					Thing_Remove(0); 
				}
				else
				{
					A_GauntletsCheckCharacter;
							 if (CountInv("CharacterClassCheck") == 0) { return state("DeggarisPickup"); }
					else if (CountInv("CharacterClassCheck") == 1) { return state("IlluciaPickup"); }
					else if (CountInv("CharacterClassCheck") == 2) { return state("FloraPickup"); }
					else if (CountInv("CharacterClassCheck") == 9) { return state("AilishPickup"); }
					return state("");
				}
				return state("");
			}
*/
			WGNT A 0
			{
				A_GiveInventory("ActorVar1",1);
				if (CountInv("ActorVar1") >= 70) { return state("SpawnSoundEmit"); }
				return state("");
			}
			loop

		Pickup:
			// Other Things
			TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("CheckPlayerIfDead") == 1,2)
			TNT1 A 0 A_PlaySound("generalequip", 5)
			TNT1 A 0 
			Stop
		Deselect:
			GNTF A 1 
			{
				A_Lower(ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035)*3);
				A_Lower(12);
			}
			Loop
		Select:
			GNTF A 1 
			{
				A_Raise(ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035)*3);
				A_Raise(12);
			}
			Loop
		Ready:
			GNTF A 1
			{
				A_WeaponReady(WRF_ALLOWUSER1);
				if (random(1,100) <= 50) { A_ChangeFlag("WEAPON.MELEEWEAPON", TRUE); } else { A_ChangeFlag("WEAPON.MELEEWEAPON", FALSE); }
			}
			Loop
		Fire:
			TNT1 A 0 A_Jump(244,2)
			TNT1 A 0 A_AlertMonsters // Sometimes a swing will be enough to wake up all monsters in the area.
			TNT1 A 0 
			{
				A_GetPunchStaminaUse(1); A_UseStaminaAttack(CountInv("StamConsumed", AAPTR_PLAYER1),2);
				if (CountInv("StamUsed", AAPTR_PLAYER1) == 1) { return state("PunchSuccess"); }
				return state("");
			}
			goto Ready
		PunchSuccess:
			TNT1 A 0
			TNT1 A 0 A_Jump(224,2)
			TNT1 A 0 A_PlaySound("PetraMediumAttack",CHAN_VOICE)
			GNTP M 3
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(2); }
				A_PlaySound("GauntletSwing",CHAN_WEAPON);
			}
			GNTP N 3
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 2) { A_SetTics(2); }
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 4) { A_SetTics(1); }
			}
			TNT1 A 0 
			{
				ACS_NamedExecuteAlways("StartTensionCountDown");
				A_GetPunchDamage(0);
				A_GetMeleeRange(2);
				A_SpreadAuraCustomPunch((CountInv("FinalMeleeDamageDealt",AAPTR_PLAYER1)),1,CPF_NOTURN,"GauntletPuff",(CountInv("MeleeAtkRange",AAPTR_PLAYER1)),0,0,"","","");

				A_TakeInventory("MeleeDestroyProjectiles",9999,0,AAPTR_PLAYER1);
				if (CallACS("MiscVarCheckDECORATE",10035) == 0 && random(1,16) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CallACS("MiscVarCheckDECORATE",10035) == 1 && random(1,8) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CallACS("MiscVarCheckDECORATE",10035) == 2 && random(1,4) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CallACS("MiscVarCheckDECORATE",10035) == 3 && random(1,2) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CallACS("MiscVarCheckDECORATE",10035) >= 4 && random(1,1) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CountInv("MeleeDestroyProjectiles",AAPTR_PLAYER1) > 0)
				{
					A_SpawnItemEx ("ShieldPartPunchAttackNoReflect", 24, 0, 32, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
					A_SpawnItemEx ("ShieldPartPunchAttackNoReflect", 56, 0, 32, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
				}
				A_TakeInventory("MeleeDestroyProjectiles",9999,0,AAPTR_PLAYER1);
			}
			GNTP O 2
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(1); }
			}
			GNTP N 3
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(2); }
			}
			GNTP M 3
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 2) { A_SetTics(2); }
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 4) { A_SetTics(1); }
				A_TakeInventory("MartArtsMaster1",9999);
				A_TakeInventory("MartArtsMaster2",9999);
				A_ReFire;
			}
			GNTP B 0 A_TakeInventory("UsingMeleeAttack",9999,0,AAPTR_PLAYER1)
			Goto Ready

		AltFire:
			TNT1 A 0 A_Jump(200,2)
			TNT1 A 0 A_AlertMonsters // Sometimes a swing will be enough to wake up all monsters in the area.
			TNT1 A 0
			{
				A_GetPunchStaminaUse(2); A_UseStaminaAttack(CountInv("StamConsumed", AAPTR_PLAYER1),2);
				if (CountInv("StamUsed", AAPTR_PLAYER1) == 1) { return state("AltPunchSuccess"); }
				return state("");
			}
			goto Ready
		AltPunchSuccess:
			TNT1 A 0
			{
				A_GiveInventory("UsingMeleeAttack",1);
				if (CallACS("MiscVarCheckDECORATE",10035) >= 5) { A_GiveInventory("MartArtsMaster2",1); }
				if (CallACS("MiscVarCheckDECORATE",10035) == 4) { A_GiveInventory("MartArtsMaster1",1); }
			}
			GNTP M 3
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(2); }
				A_PlaySound("PetraHeavyAttack",CHAN_VOICE,10.0);
				A_PlaySound("GauntletSwing",CHAN_WEAPON);
			}
			GNTP N 3
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 2) { A_SetTics(2); }
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 4) { A_SetTics(1); }
			}
			TNT1 A 0 
			{
				ACS_NamedExecuteAlways("StartTensionCountDown");
				A_GetPunchDamage(1);
				A_GetMeleeRange(3);
				A_SpreadAuraCustomPunch((CountInv("FinalMeleeDamageDealt",AAPTR_PLAYER1)),1,CPF_NOTURN,"GauntletPuff",(CountInv("MeleeAtkRange",AAPTR_PLAYER1)),0,0,"","","");

				A_TakeInventory("MeleeDestroyProjectiles",9999,0,AAPTR_PLAYER1);
				if (CallACS("MiscVarCheckDECORATE",10035) == 0 && random(1,16) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CallACS("MiscVarCheckDECORATE",10035) == 1 && random(1,8) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CallACS("MiscVarCheckDECORATE",10035) == 2 && random(1,4) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CallACS("MiscVarCheckDECORATE",10035) == 3 && random(1,2) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CallACS("MiscVarCheckDECORATE",10035) >= 4 && random(1,1) == 1) { A_GiveInventory("MeleeDestroyProjectiles",1,AAPTR_PLAYER1); }
				if (CountInv("MeleeDestroyProjectiles",AAPTR_PLAYER1) > 0)
				{
					A_SpawnItemEx ("ShieldPartPunchAttackNoReflect", 32, 0, 32, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
					A_SpawnItemEx ("ShieldPartPunchAttackNoReflect", 64, 0, 32, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
				}
				A_TakeInventory("MeleeDestroyProjectiles",9999,0,AAPTR_PLAYER1);
			}
			GNTP O 2
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(1); }
			}
			GNTP P 3
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(2); }
			}
			GNTP Q 1
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(0); }
			}
			GNTP R 1
			{
			}
			GNTP S 1
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(0); }
			}
			GNTP T 1
			{
			}
			GNTP U 1
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(0); }
			}
			GNTP V 1
			{
			}
			GNTP W 7
			{
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 2) { A_SetTics(6); }
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 3) { A_SetTics(5); }
				if (ACS_NamedExecuteWithResult("MiscVarCheckDECORATE",10035) >= 4) { A_SetTics(4); }
				A_TakeInventory("MartArtsMaster1",9999);
				A_TakeInventory("MartArtsMaster2",9999);
			}
			GNTP W 0 A_TakeInventory("UsingMeleeAttack",9999,0,AAPTR_PLAYER1)
			Goto Ready
	}
}